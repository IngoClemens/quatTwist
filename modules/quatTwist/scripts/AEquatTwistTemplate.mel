// ---------------------------------------------------------------------
// AEquatTwistTemplate.mel
//
// Attribute editor template for the quatTwist node.
//
// ---------------------------------------------------------------------

global proc AEquatTwistTemplateResetButton( string $nodeName )
{
    string $items[];
    tokenize $nodeName "." $items;
    string $form = `formLayout`;
    string $btn = `button -label "Reset"
                          -width 68
                          -command ("quatTwistPerformResetTwist " + $items[0])
                          quatTwistAEResetButton`;
    formLayout -edit
               -attachForm $btn "top" 5
               -attachForm $btn "left" 148
               $form;
}


global proc AEquatTwistTemplateResetButtonReplace( string $nodeName )
{
    string $items[];
    tokenize $nodeName "." $items;
    button -edit
           -command ("quatTwistPerformResetTwist " + $items[0])
           quatTwistAEResetButton;
}


global proc AEquatTwistTemplate( string $nodeName )
{
    // the following controls will be in a scrollable layout
    editorTemplate -beginScrollLayout;

        editorTemplate -beginLayout "QuatTwist Attributes" -collapse 0;
            editorTemplate -beginNoOptimize;
            editorTemplate -addControl "active";
            editorTemplate -addControl "axis";
            editorTemplate -addControl "invertTwist";
            editorTemplate -endNoOptimize;
            editorTemplate -addSeparator;
            editorTemplate -addControl "offset";
            editorTemplate -addControl "scale";
        editorTemplate -endLayout;

        editorTemplate -beginLayout "Output" -collapse 0;
            editorTemplate -addControl "twist";
        editorTemplate -endLayout;

        editorTemplate -beginLayout "Segments" -collapse 1;
            editorTemplate -addControl "segmentCount";
            editorTemplate -beginNoOptimize;
            editorTemplate -addControl "reverseSegments";
            editorTemplate -addSeparator;
            editorTemplate -addControl "useCurve";
            editorTemplate -endNoOptimize;
            AEaddRampControl( $nodeName + ".segmentBlend" );
            editorTemplate -addControl "twistSegmentOut";
        editorTemplate -endLayout;

        editorTemplate -beginLayout "Extension" -collapse 1;
            editorTemplate -addControl "autoRegulate";
            editorTemplate -addControl "regulationAngle";
            editorTemplate -callCustom "AEquatTwistTemplateResetButton" "AEquatTwistTemplateResetButtonReplace" $nodeName;
        editorTemplate -endLayout;

        editorTemplate -beginLayout "Reference" -collapse 1;
            editorTemplate -addControl "transformIndex";
            editorTemplate -addControl "twistInput";
        editorTemplate -endLayout;

        // include/call base class/node attributes
        AEdependNodeTemplate $nodeName;

        // add any extra attributes that have been added
        editorTemplate -addExtraControls;

    editorTemplate -endScrollLayout;
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2021 Ingo Clemens, brave rabbit
// quatTwist is under the terms of the MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------

// ---------------------------------------------------------------------
// quatTwistDelete.mel
//
// Delete the quatTwist setup for the selected transforms.
//
// ---------------------------------------------------------------------

global proc quatTwistDelete()
{
    string $sel[] = `ls -selection -transforms`;
    if (!size($sel))
        return;

    for ($s in $sel)
    {
        string $reader[] = `listConnections -source 0
                                            -destination 1
                                            -type "quatTwist"
                                            ($s + ".worldMatrix[0]")`;
        string $conn[] = `listConnections -source 0
                                          -destination 1
                                          -skipConversionNodes 1
                                          ($reader[0] + ".twistSegmentOut")`;
        string $ctrls[] = `listConnections -source 1
                                           -destination 0
                                           -skipConversionNodes 1
                                           ($reader[0] + ".twistInput")`;
        if (size($conn))
            delete $conn;

        if (`objExists $reader[0]`)
            delete $reader[0];

        if (`objExists ($s + "_twist_grp")`)
            delete ($s + "_twist_grp");

        if (`attributeQuery -exists -node $s quatTwist`)
            deleteAttr -at quatTwist $s;

        for ($c in $ctrls)
        {
            if (`attributeQuery -exists -node $c quatTwist`)
                deleteAttr -attribute quatTwist $c;
        }
    }
}

// ---------------------------------------------------------------------
// MIT License
//
// Copyright (c) 2020 Ingo Clemens, brave rabbit
// quatTwist is under the terms of the MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining
// a copy of this software and associated documentation files (the
// "Software"), to deal in the Software without restriction, including
// without limitation the rights to use, copy, modify, merge, publish,
// distribute, sublicense, and/or sell copies of the Software, and to
// permit persons to whom the Software is furnished to do so, subject to
// the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
// IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
// CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
// TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
// SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Author: Ingo Clemens    www.braverabbit.com
// ---------------------------------------------------------------------
